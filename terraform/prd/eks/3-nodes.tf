resource "aws_iam_role" "nodes" {
  name = "${aws_eks_cluster.eks.name}-nodes"

  assume_role_policy = <<POLICY
{
  "Version": "2012-10-17",
  "Statement": [
    {
      "Effect": "Allow",
      "Action": "sts:AssumeRole",
      "Principal": {
        "Service": "ec2.amazonaws.com"
      }
    }
  ]
}
POLICY
}

resource "aws_iam_role_policy_attachment" "amazon_eks_worker_node_policy" {
  policy_arn = "arn:aws:iam::aws:policy/AmazonEKSWorkerNodePolicy"
  role       = aws_iam_role.nodes.name
}

resource "aws_iam_role_policy_attachment" "amazon_eks_cni_policy" {
  policy_arn = "arn:aws:iam::aws:policy/AmazonEKS_CNI_Policy"
  role       = aws_iam_role.nodes.name
}

resource "aws_iam_role_policy_attachment" "amazon_ec2_container_registry_read_only" {
  policy_arn = "arn:aws:iam::aws:policy/AmazonEC2ContainerRegistryReadOnly"
  role       = aws_iam_role.nodes.name
}

resource "aws_iam_role_policy_attachment" "amazon_ssm_managed_instance_core" {
  policy_arn = "arn:aws:iam::aws:policy/AmazonSSMManagedInstanceCore"
  role       = aws_iam_role.nodes.name
}

resource "aws_launch_template" "eks_general" {
  name = "${aws_eks_cluster.eks.name}-general-lt"
  block_device_mappings {
    device_name = "/dev/xvda"
    ebs {
      volume_type = "gp3"
      volume_size = 50
    }
  }
  tag_specifications {
    resource_type = "instance"

    tags = {
      "Project"     = var.project,
      "Environment" = var.environment,
      "Team"        = var.team,
      "Deployedby"  = var.deployedby,
      "Application" = var.application,
      "OwnerEmail"  = var.email
      "Name"        = "${aws_eks_cluster.eks.name}-general"
    }
  }
  lifecycle {
    create_before_destroy = true
  }
}

# data "aws_ssm_parameter" "eks_ami_release_version" {
#   name = "/aws/service/eks/optimized-ami/${aws_eks_cluster.eks.version}/amazon-linux-2/recommended/release_version"
# }

resource "aws_eks_node_group" "general" {
  cluster_name = aws_eks_cluster.eks.name
  version      = local.eks_version
  # release_version = nonsensitive(data.aws_ssm_parameter.eks_ami_release_version.value)
  release_version = "1.31.0-20241024"
  node_group_name = "general"
  node_role_arn   = aws_iam_role.nodes.arn

  subnet_ids = [
    # data.terraform_remote_state.vpc.outputs.private_subnet1_id,
    # data.terraform_remote_state.vpc.outputs.private_subnet2_id
    "subnet-0460696ffc4572245", "subnet-046948acbc7f8fdd9"
  ]

  capacity_type  = "ON_DEMAND"
  instance_types = ["t3a.large"]

  scaling_config {
    desired_size = 2
    max_size     = 3
    min_size     = 1
  }

  update_config {
    max_unavailable = 1
  }

  labels = {
    role      = "general"
    az        = var.aws_region
    lifecycle = "OnDemand"
  }

  launch_template {
    id      = aws_launch_template.eks_general.id
    version = aws_launch_template.eks_general.latest_version
  }

  depends_on = [
    aws_iam_role_policy_attachment.amazon_eks_worker_node_policy,
    aws_iam_role_policy_attachment.amazon_eks_cni_policy,
    aws_iam_role_policy_attachment.amazon_ec2_container_registry_read_only
  ]

  lifecycle {
    create_before_destroy = true
  }
}
